import org.gradle.api.java.archives.internal.DefaultManifest

buildscript {
    dependencies {
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "java" // java plugin is needed for the shadow plugin to work
    id "eclipse"
    id "maven-publish"
    id "net.minecraftforge.gradle" version "4.1.+"
}
apply plugin: "org.spongepowered.mixin"

version = "1.16.x-2.0.1"
group = "dk.zlepper.itlt" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "itlt"

// Target Java 8 but use a Java 11 compiler
java.toolchain.languageVersion = JavaLanguageVersion.of(8)
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"
tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
    sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

    // You can verify this is working correctly by running the build task, extracting itlt.class from
    // the built jar and checking if the bytecode version is 52 using the command "javap -verbose ./itlt.class"
}

// Use Java 8 for run tasks (e.g. runClient)
tasks.withType(JavaExec).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(8)
    }

    // You can verify this is working correctly by running the runClient task and checking the debug.log
    // in the run folder for "[dk.zlepper.itlt.itlt/]: javaVerInt: 8"
}

println "Java: ${System.getProperty "java.version"}, JVM: ${System.getProperty "java.vm.version"} (${System.getProperty "java.vendor"}), Arch: ${System.getProperty "os.arch"}"
minecraft {
    mappings channel: "snapshot", version: "20200723-1.16.1"

    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        final File runFile = project.file("run")
        client {
            workingDirectory runFile
            property "forge.logging.console.level", "info"
            arg "-mixin.config=itlt.mixins.json"
            mods {
                itlt {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory runFile
            property "forge.logging.console.level", "info"
            arg "-mixin.config=itlt.mixins.json"
            mods {
                itlt {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory runFile
            property "forge.logging.console.level", "info"
            args "--mod", "itlt", "--all", "--output", file("src/generated/resources/")
            mods {
                itlt {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shade
}

repositories {
    maven {
        url = "https://cursemaven.com"
        content { includeGroup "curse.maven" }
    }
    mavenCentral()
    maven { url = "https://repository.mulesoft.org/nexus/content/repositories/public/" }
}

dependencies {
    // Forge
    minecraft "net.minecraftforge:forge:1.16.5-36.2.28"

    // Mixin
    implementation "org.spongepowered:mixin:0.8.2"
    annotationProcessor "org.spongepowered:mixin:0.8.2:processor"

    // itlt uses the "image4j" library to read .ico files
    implementation "com.github.imcdonagh:image4j:0.7.2"
    shade "com.github.imcdonagh:image4j:0.7.2"

    // itlt uses the "Apache Commons Imaging" library to read .icns files
    /*implementation "org.apache.commons:commons-imaging:1.0-alpha2"
    shade "org.apache.commons:commons-imaging:1.0-alpha2"*/
    // due to it being such a big library and not supporting class removal, I've made a single method return null,
    // removed jpeg, tiff, examples and tests and then compiled it. This fork supports removing classes for formats we
    // don't use, therefore allowing us to drastically reduce the shrunk jar's filesize
    implementation files("thirdPartyLibs/commons-imaging-1.0-alpha2-custom.jar")
    shade files("thirdPartyLibs/commons-imaging-1.0-alpha2-custom.jar")
}

ext {
    MANIFEST = manifest {
        attributes([
                "Specification-Title": "itlt",
                "Specification-Vendor": "Paint_Ninja",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" : "Paint_Ninja",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "itlt.mixins.json",
                "Main-Class": "dk.zlepper.itlt.Main" // so that the pop-up window code works
        ])
    } as DefaultManifest
}

jar {
    manifest.from(MANIFEST)
}

shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.shade]

    // Apache Commons Imaging needs to be put somewhere else to prevent a NoClassDefFoundError
    relocate "org.apache.commons.imaging", "${project.group}.shadow.org.apache.commons.imaging"

    manifest.from(MANIFEST)
}

reobf {
    shadowJar { }
}

task shrinkJar(type: Jar, dependsOn: reobfShadowJar) {
    archiveClassifier.set("shrunk")

    final File inputJar = file("${buildDir}/libs/${archivesBaseName}-${project.version}.jar")
    final String shadowGroup = project.group.replace('.','/') + "/shadow"
    final String apacheImaging = "${shadowGroup}/org/apache/commons/imaging"

    from zipTree(inputJar)

    exclude "net/sf/image4j/example/**", "net/sf/image4j/test/**"

    exclude "${apacheImaging}/formats/gif/**", "${apacheImaging}/formats/psd/**", "${apacheImaging}/formats/rgbe/**",
            "${apacheImaging}/formats/xbm/**", "${apacheImaging}/formats/xpm/**", "${apacheImaging}/formats/pnm/**",
            "${apacheImaging}/formats/pcx/**", "${apacheImaging}/formats/dcx/**", "${apacheImaging}/formats/png/**",
            "${apacheImaging}/formats/bmp/**", "${apacheImaging}/formats/wbmp/**", "${apacheImaging}/formats/ico/**"

    exclude "${apacheImaging}/common/itu_t4/**", "${apacheImaging}/common/mylzw/**", "${apacheImaging}/common/BasicCParser**",
            "${apacheImaging}/common/RationalNumber**", "${apacheImaging}/common/GenericImageMetadata**",
            "${apacheImaging}/common/ZlibDeflate**", "${apacheImaging}/common/PackBits**", "${apacheImaging}/icc/**",
            "${apacheImaging}/palette/**", "${apacheImaging}/color/**", "${apacheImaging}/ImageDump**",
            "${apacheImaging}/ColorTools**", "${apacheImaging}/internal/**"

    exclude "META-INF/LICENSE.txt", "META-INF/NOTICE.txt"

    manifest.from(MANIFEST)
}

//tasks.build.dependsOn reobfShadowJar
//jar.finalizedBy reobfShadowJar
tasks.build.dependsOn shrinkJar
jar.finalizedBy shrinkJar

mixin {
    add sourceSets.main, "itlt.refmap.json"
}